

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.github.pacifistmc.forgix:Forgix:1.2.9"
    }
}

plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.6+" apply false
    id "me.shedaniel.unified-publishing" version "0.1.+"
    id "com.github.breadmoirai.github-release" version "2.4.1"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            // parchment("org.parchmentmc.data:parchment-${rootProject.mappings}")
        }
        implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    }
}


def forgeDir = new File(project.projectDir, 'forge/build/libs/')
def fabricDir = new File(project.projectDir, 'fabric/build/libs/')
def commonDir = new File(project.projectDir, 'common/build/libs/')

def filter = { File file -> file.getName().contains(mod_version) } as FileFilter

def forgeFiles = forgeDir.listFiles(filter)
def fabricFiles = fabricDir.listFiles(filter)
def commonFiles = commonDir.listFiles(filter)

githubRelease {
    token project.findProperty("github") ?: System.getenv("github") ?: ""
    owner "WhoCraft"
    repo "TardisRefined"
    tagName "v$rootProject.minecraft_version-$project.mod_version"
    targetCommitish "mc/1.20.1"
    releaseName "[$rootProject.minecraft_version] Tardis Refined - v$project.mod_version"
    generateReleaseNotes true
    body new File("${rootProject.projectDir}/changelog.md").text
    draft false
    prerelease false
    releaseAssets(forgeFiles + fabricFiles + commonFiles)
    allowUploadToExisting.set false
    overwrite true
    dryRun false
    apiEndpoint "https://api.github.com"
    client
}



allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = "${project.name}-${rootProject.jar_name}"
    version = "mc${rootProject.minecraft_version}-v${rootProject.mod_version}"
    group = rootProject.maven_group

    repositories {

        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }

        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
        }

        maven {
            url "https://cursemaven.com"
        }

        maven { url 'https://jitpack.io' }

        flatDir {
            dirs '../mods'
        }


    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

task publishMeEverywhere(){
    dependsOn ':forge:publishUnified'
    dependsOn ':fabric:publishUnified'
    dependsOn ':githubRelease'
}


def generatedResources = project(":forge").file("src/generated/resources")

sourceSets {
    main {
        resources.srcDir generatedResources
    }
}

